plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
}

android {
    namespace 'rectangledbmi.com.pittsburghrealtimetracker'

    buildToolsVersion versions.android.buildTools
    compileSdkVersion versions.android.targetSdk
    defaultConfig {
        applicationId "rectangledbmi.com.pittsburghrealtimetracker"
        minSdkVersion versions.android.minSdk
        targetSdkVersion versions.android.targetSdk
        versionCode 160090100
        versionName "9.1.0-alpha"
    }
    buildTypes {

        debug {
            buildConfigField 'String', 'PAT_API_KEY', keys.truetime.debug // variable defined in `../build.gradle`
            minifyEnabled false
            resValue "string", "google_maps_key", keys.google.maps.debug // variable defined in `../build.gradle`
        }
        release {
            buildConfigField 'String', 'PAT_API_KEY', keys.truetime.release // variable defined in `../build.gradle`
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "google_maps_key", keys.google.maps.release // variable defined in `../build.gradle`
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility versions.java.target
        targetCompatibility versions.java.target
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }
    kotlinOptions {
        jvmTarget = versions.java.version
    }
    lint {
        warning 'InvalidPackage'
    }
    // java 17 mockito issue: https://stackoverflow.com/questions/68113065/mockito-inaccessible-object-exception-while-creating-a-mock-object
    // something up with gson lol
    // TODO: if we change mockito libs/update or swap out GSON, test with this removed
    testOptions {
        unitTests.all {
            jvmArgs '--add-opens=java.base/java.lang=ALL-UNNAMED'
        }
    }
}

dependencies {
    coreLibraryDesugaring libs.android.desugarer

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':pat-static')

    implementation libs.bundles.android
    implementation libs.bundles.android.app
    implementation libs.bundles.google.play

    implementation libs.bundles.restcalls

    implementation libs.rx.android
    implementation libs.rx.java
    implementation libs.rx.network

    testImplementation libs.bundles.testing

    //3rd party android libraries

    implementation libs.kotlin.stdlib
    implementation libs.javax.annotations

    debugImplementation libs.square.leakcanary

}
